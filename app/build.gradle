plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.cloud.tools.jib' version '3.4.5'
	id 'com.github.node-gradle.node' version '7.1.0'
}

group = 'doo.dah.aui'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springAiVersion', "1.0.0")
	set('sqliteJdbcVersion', "3.49.1.0")
	set('hibernateDialectsVersion', "6.6.8.Final")
	set('jsoupVersion', "1.18.3")
	set('tikaVersion', "3.1.0")
	set('pdfboxVersion', "3.0.4")
	set('poiVersion', "5.2.5")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.ai:spring-ai-starter-model-ollama'
	implementation 'org.springframework.ai:spring-ai-advisors-vector-store'
	implementation 'org.springframework.ai:spring-ai-starter-model-chat-memory'
	implementation 'org.springframework.ai:spring-ai-starter-vector-store-pinecone'
	implementation "org.xerial:sqlite-jdbc:${sqliteJdbcVersion}"
	implementation "org.hibernate.orm:hibernate-community-dialects:${hibernateDialectsVersion}"
	implementation "org.jsoup:jsoup:${jsoupVersion}" // or the latest version
	// Document processing dependencies
	implementation "org.apache.tika:tika-core:${tikaVersion}"
	implementation "org.apache.tika:tika-parsers-standard-package:${tikaVersion}"
	implementation "org.apache.pdfbox:pdfbox:${pdfboxVersion}"
	implementation "org.apache.poi:poi:${poiVersion}"
	implementation "org.apache.poi:poi-ooxml:${poiVersion}"
	implementation "org.apache.poi:poi-scratchpad:${poiVersion}"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.ai:spring-ai-spring-boot-testcontainers'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Configure the Node.js plugin
node {
	version = '20.11.0'
	download = true
	workDir = file('../ui')
	nodeProjectDir = file('../ui')
}

// Task to copy UI build output to Spring Boot static resources
task copyUI(type: Copy, dependsOn: 'npm_run_build') {
	from '../ui/dist'
	into 'src/main/resources/static'
}

// Make processResources depend on the UI build so it's included in the JAR
tasks.named('processResources').configure {
	dependsOn copyUI
}

jib {
	from {
		image = 'openjdk:24-jdk-slim'
	}
	to {
		image = '660315378336.dkr.ecr.us-east-2.amazonaws.com/doo-dah-aui'
		tags = ['latest', version]
	}
	container {
		ports = ['8080']
		mainClass = 'doo.dah.aui.artificial_unintelligence.ArtificialUnIntelligenceApplication'
		jvmFlags = ['-Xms512m', '-Xmx1024m']
		environment = [
			'SPRING_PROFILES_ACTIVE': 'aws'
		]
	}
	allowInsecureRegistries = true
}
